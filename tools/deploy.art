do ./"versioncontrol.art"

packages: # "packages/list.art"
do relative "getrepodetails.art"

do ::
    cnt: 1
    total: size packages

    hasError?: false
    problemWithRepo: new []
    problemWithReleases: new []

    separator: repeat "=" 80

    print separator
    print " Deploying packages"
    print separator

    listWithDescriptions: #[]

    loop packages [name,pkg][
        prints [pad to :string cnt 4 "/" pad.right to :string total 4 "|"]
        prints color #cyan "Processing: "

        pkgType: "gh"
        if is? :codebergLink pkg -> pkgType: "cb"
        prints color #magenta pad.right pkgType 3

        prints pad.right name 38

        [owner,repo]: split.by:"/" pkg\location
        
        targetName: ~"|name|.art"
        targetFolder: "packages"

        infoD: getRepoInfo pkg
        write express.unwrapped.pretty infoD ~"|targetFolder|/info/|targetName|" 

        versionD: getVersions pkg
        write express.pretty versionD ~"|targetFolder|/version/|targetName|" 

        print color #green "[ âœ…  OK   ]"  

        listWithDescriptions\[name]: #[
            description: infoD\description
            tags: infoD\tags
        ]
                    
        cnt: cnt + 1
    ]

    write express.unwrapped.pretty listWithDescriptions ~"public/list.art" 

    print separator
    print ""
    print [">" color #green "Everything went fine! ğŸ”¥ğŸ”¥ğŸ”¥"]
    exit